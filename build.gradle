plugins {
    id 'java'
    alias libs.plugins.native
    alias libs.plugins.git.properties
    alias libs.plugins.spring.boot
}

group = 'com.huyiyu'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    maven {
        url = uri('https://maven.aliyun.com/repository/public')
    }
}

gitProperties {
    keys = ['git.build.version', 'git.commit.user.name', 'git.commit.message.short', 'git.commit.time']
    dateFormat = 'yyyy-MM-dd HH:mm:ss'
    dateFormatTimeZone = 'CST'
}


dependencies {
    implementation enforcedPlatform(libs.spring.boot.dependency)
    annotationProcessor enforcedPlatform(libs.spring.boot.dependency)
    compileOnly 'org.projectlombok:lombok'
    compileOnly 'org.slf4j:slf4j-api'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor libs.picocli.codegen
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    implementation libs.picocli.boot
    implementation libs.flyway.mysql
    implementation 'org.springframework:spring-web'
    implementation 'com.mysql:mysql-connector-j'
    implementation libs.commons.lang3
    implementation libs.gson
    implementation libs.diffutils
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}
compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += ['-parameters', "-Aproject=${project.group}/${project.name}"]
}


bootJar {
    exclude '**/db', '**/chart', '**/application.properties'
}

test {
    useJUnitPlatform()
}


graalvmNative {
    binaries {
        main {
            verbose = true
            debug = true
            def native_enc = org.gradle.internal.os.OperatingSystem.current().isWindows() ? 'GBK' : 'UTF-8'
            buildArgs.addAll([
                    '-H:+AddAllCharsets',
                    '-H:IncludeLocales=zh',
                    "-Dfile.encoding=${native_enc}",
                    '--enable-url-protocols=https'])
            sharedLibrary = false
            mainClass = 'com.huyiyu.deploy.DeployToolApplication'
            name = 'deploy-tool'

        }
    }
}